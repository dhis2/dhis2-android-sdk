/*
 * Copyright (c) 2016, University of Oslo
 *
 * All rights reserved.
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * Redistributions of source code must retain the above copyright notice, this
 * list of conditions and the following disclaimer.
 *
 * Redistributions in binary form must reproduce the above copyright notice,
 * this list of conditions and the following disclaimer in the documentation
 * and/or other materials provided with the distribution.
 * Neither the name of the HISP project nor the names of its contributors may
 * be used to endorse or promote products derived from this software without
 * specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR
 * ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
 * ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply plugin: "com.android.library"
apply from: rootProject.file("plugins/android-checkstyle.gradle")
apply from: rootProject.file("plugins/android-findbugs.gradle")
apply from: rootProject.file("plugins/android-pmd.gradle")

def configuration = rootProject.ext.configuration
def libraries = rootProject.ext.libraries

android {
    compileSdkVersion configuration.targetSdkVersion
    buildToolsVersion configuration.buildToolsVersion

    defaultConfig {
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode configuration.versionCode
        versionName configuration.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        // excluding duplicate license files
        // from jackson modules
        exclude "META-INF/LICENSE"

        // should be removed with interoperability library
        exclude "META-INF/rxjava.properties"
    }

    lintOptions {
        // casting 'InvalidPackage' error to warning
        warning "InvalidPackage"

        // Fail early.
        abortOnError true
    }

    buildTypes {
        debug {
            // a fix for the debugger not being able to find local scope variables
            testCoverageEnabled = false
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
            resources.srcDirs += ['src/sharedTest/resources']
        }
        androidTest {
            java.srcDirs sharedTestDir
            resources.srcDirs += ['src/sharedTest/resources']
        }

    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}


dependencies {

    // sqlbrite-migrations library
    implementation "com.github.lykmapipo:sqlbrite-migrations:${libraries.sqlbritemigrations}"

    // Support libraries
    implementation "com.android.support:support-annotations:${libraries.support}"

    // Google's auto-value. Annotations are used only during compilation time, as well as

    // annotation processor which hoops up into compilation process
    compileOnly "com.google.auto.value:auto-value:${libraries.autovalue}"
    annotationProcessor "com.google.auto.value:auto-value:${libraries.autovalue}"

    // Dagger
    implementation "com.google.dagger:dagger:${libraries.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${libraries.dagger}"

    // Jackson
    implementation "com.fasterxml.jackson.core:jackson-databind:${libraries.jackson}"

    // Google's play services
    implementation "com.google.android.gms:play-services-safetynet:${libraries.safetynet}"

    // Square libraries
    implementation "com.squareup.okhttp3:okhttp:${libraries.okhttp}"
    implementation "com.squareup.retrofit2:retrofit:${libraries.retrofit}"
    implementation "com.squareup.retrofit2:converter-jackson:${libraries.retrofit}"

    // Apache libraries
    implementation "org.apache.commons:commons-jexl:2.1.1"
    implementation "org.apache.commons:commons-lang3:3.7"

    // Joda time
    implementation "joda-time:joda-time:2.9.9"

    // Extension which generates mappers for work with cursor and content values
    implementation "com.gabrielittner.auto.value:auto-value-cursor-annotations:${libraries.autovaluecursor}"
    annotationProcessor "com.gabrielittner.auto.value:auto-value-cursor:${libraries.autovaluecursor}"

    // Java test dependencies
    testImplementation "junit:junit:${libraries.junit}"
    testImplementation "org.mockito:mockito-core:${libraries.mockito}"
    testImplementation "org.assertj:assertj-core:${libraries.assertj}"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:${libraries.equalsverifier}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${libraries.okhttp}"
    testImplementation "com.google.code.findbugs:annotations:${libraries.findbugs}"
    testImplementation "com.android.support.test:runner:${libraries.testrunner}"

    // Android test dependencies
    androidTestImplementation "org.mockito:mockito-core:${libraries.mockito}"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${libraries.okhttp}"
    androidTestImplementation "com.android.support.test:runner:${libraries.testrunner}"
    androidTestImplementation "com.squareup.okhttp3:logging-interceptor:${libraries.okhttp}"
    androidTestImplementation("com.google.truth:truth:${libraries.truth}") {
        exclude group: 'junit' // Android has JUnit built in.
    }
    androidTestImplementation("com.facebook.stetho:stetho:${libraries.stetho}") {
        exclude module: 'jsr305'
    }
    androidTestImplementation("com.facebook.stetho:stetho-okhttp3:${libraries.stetho}") {
        exclude module: 'okhttp'
        exclude module: 'jsr305'
    }
    implementation "com.google.code.findbugs:annotations:${libraries.findbugs}"
}

apply from: rootProject.file("plugins/gradle-mvn-push.gradle")
