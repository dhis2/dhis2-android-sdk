/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "maven"
apply plugin: "signing"

def isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

def getReleaseRepositoryUrl() {
    return hasProperty("RELEASE_REPOSITORY_URL") ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty("SNAPSHOT_REPOSITORY_URL") ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty("NEXUS_USERNAME") ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty("NEXUS_PASSWORD") ? NEXUS_PASSWORD : ""
}

def gpgKeyId() {
    return hasProperty("GPG_KEY_ID") ? GPG_KEY_ID : ""
}

def gpgKeyLocation() {
    return hasProperty("GPG_KEY_LOCATION") ? GPG_KEY_LOCATION : ""
}

def gpgPassphrase() {
    return hasProperty("GPG_PASSPHRASE") ? GPG_PASSPHRASE : ""
}

def tempBuildFolder = "$rootDir/build/deploy"

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects { ext."signing.keyId" = gpgKeyId() }
        allprojects { ext."signing.secretKeyRingFile" = gpgKeyLocation() }
        allprojects { ext."signing.password" = gpgPassphrase() }
    }
}

afterEvaluate { project ->

    uploadArchives {
        doFirst {
            mkdir "file:/$tempBuildFolder"
        }
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                // Hack: Deploy to a local repository first, then upload to Sonatype using scripts.
                repository(url: "file://localhost/$tempBuildFolder") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    if (project.getPlugins().hasPlugin("com.android.application") ||
            project.getPlugins().hasPlugin("com.android.library")) {
        task install(type: Upload, dependsOn: assemble) {
            repositories.mavenInstaller {
                configuration = configurations.archives

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }

        task androidJavadocs(type: Javadoc) {
            source = android.sourceSets.main.java.source
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            exclude "org/hisp/dhis/android/core/**/internal/**"
            exclude "org/hisp/dhis/android/core/**/*InternalAccessor.java"
            exclude "org/hisp/dhis/android/core/**/**.kt"
        }

        task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
            classifier = "javadoc"
            from androidJavadocs.destinationDir
        }

        task androidSourcesJar(type: Jar) {
            classifier = "sources"
            from android.sourceSets.main.java.source
        }
    } else {
        install {
            repositories.mavenInstaller {
                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = "sources"
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = "javadoc"
            from javadoc.destinationDir
        }
    }

    artifacts {
        if (project.getPlugins().hasPlugin("com.android.application") ||
                project.getPlugins().hasPlugin("com.android.library")) {
            archives androidSourcesJar
            archives androidJavadocsJar
        } else {
            archives sourcesJar
            archives javadocJar
        }
    }

}

// Scripted hack, inpired from:
// https://github.com/osmlab/atlas/blob/dev/gradle/deployment.gradle
def uploadToNexusStaging(def username, def password, def repoDir) {
    def proc = ['./uploadToNexusStaging.sh', username, password, repoDir].execute([], file("${rootDir.toString()}/core/plugins"))
    proc.waitForProcessOutput(System.out, System.err)
}

task publishToNexus(dependsOn: 'uploadArchives') {
    doLast {
        if (isReleaseBuild()) {
            uploadToNexusStaging(getRepositoryUsername(), getRepositoryPassword(), tempBuildFolder)
        }
        // If is not a release build, artifacts are deployed in uploadArchives directive
    }
}