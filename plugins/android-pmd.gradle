apply plugin: "pmd"

pmd {
    toolVersion = "5.5.2"
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        tasks.register("pmd${variant.name.capitalize()}", Pmd) { pmdTask ->
            pmdTask.group = "verification"
            pmdTask.description = "Run Pmd for the ${variant.name}"

            // Fail early.
            pmdTask.ignoreFailures = false

            // Only html reports.
            pmdTask.reports {
                html.enabled = true
                xml.enabled = false
            }

            // see: https://pmd.github.io/pmd-5.5.2/pmd-java/rules/index.html
            pmdTask.ruleSetFiles = rootProject.files(
                    "plugins/pmd-rules.xml"
            )

            // setting list of rule sets explicitly to empty list
            pmdTask.ruleSets = []

            def compileTask = variant.javaCompile
            pmdTask.source = compileTask.source
            pmdTask.dependsOn(compileTask)

            def checkTask = tasks.getByName("check")
            checkTask.dependsOn(pmdTask)
        }
    }
}